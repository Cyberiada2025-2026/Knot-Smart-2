@tool
class_name TreeMeshGenerator
extends Node

var tree_generator: TreeGenerator


func generate_array_mesh(branch: TreeBranch) -> ArrayMesh:
	var vertices = PackedVector3Array()
	var indices = PackedInt32Array()
	var tangents = PackedFloat32Array()
	add_segments(vertices, branch, branch.sides)
	add_indices(indices, len(branch.pos_array) - 1, branch.sides)
	add_tangents(vertices, tangents)
	var arr_mesh = ArrayMesh.new()
	var arrays = []
	arrays.resize(Mesh.ARRAY_MAX)
	arrays[Mesh.ARRAY_VERTEX] = vertices
	arrays[Mesh.ARRAY_INDEX] = indices
	arrays[Mesh.ARRAY_TANGENT] = tangents
	arr_mesh.add_surface_from_arrays(Mesh.PRIMITIVE_TRIANGLE_STRIP, arrays)
	return arr_mesh


func add_segments(vertices: PackedVector3Array, branch: TreeBranch, sides: int):
	var radius = branch.radius
	for center in branch.pos_array:
		var angle: float = TAU / sides
		for i in range(sides, 0, -1):
			var vertex = Vector3(cos(i * angle) * radius, 0.0, sin(i * angle) * radius) + center
			vertices.push_back(vertex)
		radius *= branch.rate_of_shrinking


func add_indices(indices: PackedInt32Array, stripes: int, length: int):
	for i in range(stripes):
		for j in range(length):
			indices.push_back(i * length + j)
			indices.push_back((i + 1) * length + j)
		indices.push_back(i * length)
		indices.push_back((i + 1) * length)


func add_tangents(vertices: PackedVector3Array, tangents: PackedFloat32Array):
	for vertex in vertices:
		for i in range(4):
			tangents.push_back(1)
