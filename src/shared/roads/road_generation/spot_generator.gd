@tool
class_name SpotGenerator
extends Resource

var _blueprint: MapTileData
var _map_size: int
var _final_spots: Array[Spot] = []


func generate_spots(road_generator: RoadGenerator):
	_blueprint = road_generator.blueprint
	var generation_params = road_generator.generation_params
	_map_size = generation_params.map_size
	var spots: Array[Spot] = []
	# clear previous generation results
	_final_spots.clear()

	# create initial rectangle spot that will be divided into smaller ones
	spots.push_back(Spot.new(Vector2i(0, 0), Vector2i(_map_size - 1, _map_size - 1)))

	var steps_success: int = 0
	var steps_all: int = 0

	# splitting rectangles until they reach proper size
	while not spots.is_empty() and steps_all < generation_params.generation_steps_limit:
		steps_all += 1
		var curr_spot_idx: int = randi_range(0, len(spots) - 1)
		var curr_spot: Spot = spots[curr_spot_idx]

		var area = generation_params.get_area(curr_spot)

		# action decides whether we are splitting x or y direction
		var axis = Utils.Axis2.values().pick_random()

		if _split_spot(curr_spot, area, axis, spots):
			steps_success += 1

		if _is_spot_correctly_sized(curr_spot, area.max_spot_size):
			var spot = spots.pop_at(curr_spot_idx)
			if not _is_spot_touching_map_bounds(spot):
				_final_spots.push_back(spot)

		# highways are created by moving all spots's start by 1
		if steps_success == generation_params.highway_generation_split_count:
			_move_spot_start(spots)
			_move_spot_start(_final_spots)
			steps_success += 1

	if not spots.is_empty():
		if road_generator.log_generation_steps:
			print("full splits failed for ", spots.size(), " spots, moving them to final array")
		_final_spots.append_array(spots.filter(_is_spot_touching_map_bounds))

	if road_generator.log_generation_steps:
		print("Roads generated, road generation success steps: ", steps_success, " all: ", steps_all)


func cast_spots_to_blueprint():
	for spot in _final_spots:
		spot.cast_on_blueprint(_blueprint)
	# adjust spot sizes to avoid intersection with roads when they will be exported
	_move_spot_start(_final_spots)


func get_spots():
	return _final_spots


## Get coordinates of all points located between start and end positions
func _get_area_positions_array(start: Vector2i, end: Vector2i) -> Array:
	var coordinates: Array[Vector2i]
	for x in range(start.x, end.x + 1):
		for y in range(start.y, end.y + 1):
			coordinates.push_back(Vector2i(x, y))
	return coordinates


func _is_valid_tile(position: Vector2i, axis: int):
	return (
			(
				axis == Utils.Axis2.X
				and _blueprint.data[position].placement_rule == TileInfo.PlacementRule.SLOPE_X
			)
			or (
				axis == Utils.Axis2.Y
				and _blueprint.data[position].placement_rule == TileInfo.PlacementRule.SLOPE_Z
			)
			or _blueprint.data[position].placement_rule == TileInfo.PlacementRule.FLAT
		)


## Splits spot into 2 smaller ones if possible
func _split_spot(spot: Spot, area: LimiterArea, axis: int, spots: Array) -> bool:
	if spot.size()[axis] <= area.max_spot_size[axis]:
		return false

	var split_point = randi_range(
		area.min_spot_size[axis],
		spot.size()[axis] - area.min_spot_size[axis]
	)

	var e1: Vector2i = spot.end
	var s2: Vector2i = spot.start
	e1[axis] = spot.start[axis] + split_point
	s2[axis] = spot.start[axis] + split_point

	# avoid placing streets on incorrect slopes and terrain corners
	for position in _get_area_positions_array(s2, e1):
		if not _is_valid_tile(position, axis):
			return false

	var new_spot: Spot = Spot.new(s2, spot.end)
	spot.end = e1
	spots.push_back(new_spot)
	return true


func _is_spot_correctly_sized(spot: Spot, max_spot_size: Vector2i) -> bool:
	for axis in Utils.Axis2.values():
		if spot.size()[axis] > max_spot_size[axis]:
			return false
	return true


func _is_spot_touching_map_bounds(spot: Spot) -> bool:
	for axis in Utils.Axis2.values():
		if(
			spot.start[axis] == 0
			or spot.end[axis] ==  _map_size - 1
		):
			return true
	return false


## Move spots' start 1 tile forward
func _move_spot_start(spots: Array[Spot]):
	for axis in Utils.Axis2.values():
		for spot in spots:
			if spot.start[axis] != 0:
				spot.start[axis] += 1
