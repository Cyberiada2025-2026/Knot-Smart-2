class_name Rope
extends Node3D

var params: RopeParams

var rope_vfx = preload("uid://djqe8wkjmmn8n")

var vfx: RopeVFX
var rope: Area3D
var collision_shape: CapsuleShape3D

var node: Array[Node]
var link: Array[NodeLink]
var end: Array[RopeEnd]


func _init(rope_params: RopeParams, nodes: Array[Node], markers: Array[MeshInstance3D]) -> void:
	self.params = rope_params
	self.node = nodes
	self.end = []

	for i in range(2):
		var l = NodeLink.new(self)
		link.append(l)
		node[i].add_child(l)
		var strategy
		match node[i].get_class():
			"RigidBody3D":
				strategy = BasicDynamicStrategy.new(params.min_rope_length)
			"CharacterBody3D":
				strategy = BasicKinematicStrategy.new()
			_:
				strategy = BasicStaticStrategy.new()
		end.append(RopeEnd.new(self.params, strategy, markers[i]))

		add_child(end[i])


func init_rope_mesh():
	vfx = rope_vfx.instantiate()
	vfx.start(params)
	vfx.rotate_x(-PI / 2)
	rope.add_child(vfx)


func init_rope_collider():
	var direction = end[1].position - end[0].position

	collision_shape = CapsuleShape3D.new()
	collision_shape.radius = params.rope_collision_radius

	collision_shape.height = direction.length()
	var collider = CollisionShape3D.new()
	collider.shape = collision_shape
	collider.rotate_x(-PI / 2)
	rope.add_child(collider)


func _on_area_entered(body: Node3D):
	for n in self.node:
		if body.get_instance_id() == n.get_instance_id():
			return
	finish()


func finish():
	vfx.end()
	apply_forces()
	for l in link:
		l.queue_free()
	queue_free()


func fuse():
	if node[0] is RigidBody3D and node[1] is RigidBody3D:
		align_nodes()

		var final_pos = (end[0].global_position + end[1].global_position) / 2

		for i in range(2):
			var diff = node[i].global_position - end[i].global_position
			node[i].global_position = final_pos + diff

		var combined = RigidBody3D.new()
		get_node("../../../").add_child(combined)
		combined.global_position = final_pos

		for n in node:
			for child in n.get_children():
				if child is NodeLink and child.linked is Rope:
					child.linked.finish()
				else:
					child.reparent(combined)
			combined.mass += n.mass
			n.queue_free()

		vfx.on_fuse()
		finish()


func align_nodes():
	var final_pos = (end[0].global_position + end[1].global_position) / 2

	for i in range(2):
		var alignment_transfer = Node3D.new()

		# Align dummy node's forward axis to the rope end
		node[i].get_parent().add_child(alignment_transfer)
		alignment_transfer.global_position = node[i].global_position
		alignment_transfer.look_at(end[i].global_position)

		# Change rope endpoints' parents to dummy *while keeping global transform*
		# Now each reparented node is aligned with the dummy node's forward axis
		node[i].reparent(alignment_transfer)
		end[i].reparent(alignment_transfer)

		# Look at the rope midpoint. This also reorients the child nodes.
		alignment_transfer.look_at(final_pos)

		# Restore previous tree relationship while keeping the new global transform.
		node[i].reparent(alignment_transfer.get_parent())
		end[i].reparent(self)
		alignment_transfer.queue_free()


func update_rope():
	var direction = end[1].position - end[0].position
	var length = direction.length()
	vfx.set_length(length)
	collision_shape.height = length
	rope.look_at_from_position(end[0].position + direction / 2, end[0].position)


func _ready() -> void:
	end[0].pin(node[0], end[1])
	end[1].pin(node[1], end[0])

	rope = Area3D.new()
	init_rope_mesh()
	init_rope_collider()
	var direction = end[1].position - end[0].position
	rope.look_at_from_position(end[0].position + direction / 2, end[0].position)
	rope.body_entered.connect(_on_area_entered)
	add_child(rope)


func apply_forces() -> void:
	for i in range(2):
		end[i].strategy.release_force(node[i])


func _physics_process(_delta: float) -> void:
	if end[0].position.distance_squared_to(end[1].position) > pow(params.max_rope_length, 2):
		finish()

	if (
		end[0].get_strategy_type() == RopeEnd.StrategyType.STATIC
		and end[1].get_strategy_type() == RopeEnd.StrategyType.STATIC
	):
		finish()

	update_rope()
